plugins {
    id 'java'
    id 'java-library'
    id 'com.gradleup.shadow' version '9.3.0'
    id 'io.github.nickid2018.generate-mock'
}

group = 'io.github.nickid2018'
version = '1.0'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = "minecraft"
        url = "https://libraries.minecraft.net/"
    }
}

dependencies {
    api 'org.ow2.asm:asm:9.9.1'
    api 'org.ow2.asm:asm-tree:9.9.1'
    api 'org.ow2.asm:asm-commons:9.9.1'

    api 'commons-io:commons-io:2.15.0'
    api 'com.google.code.gson:gson:2.10.1'
    api 'org.apache.httpcomponents.client5:httpclient5:5.2.1'

    api "org.apache.logging.log4j:log4j-api:2.25.0"
    api 'org.apache.logging.log4j:log4j-core:2.25.3'
    api "org.apache.logging.log4j:log4j-slf4j-impl:2.25.0"

    api 'it.unimi.dsi:fastutil:8.5.12'
    api 'com.google.guava:guava:32.1.3-jre'
    api 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'
    api 'me.tongfei:progressbar:0.10.0'
    api 'io.github.nickid2018:easy-mock:1.0'

    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    compileOnly mocker.mock('./src/main/resources/remap/api.txt')
    compileOnly 'org.lwjgl:lwjgl:3.3.3'
    compileOnly 'org.lwjgl:lwjgl-glfw:3.3.3'
    compileOnly 'org.lwjgl:lwjgl-opengl:3.3.3'
    compileOnly 'org.lwjgl:lwjgl-stb:3.3.3'
    compileOnly 'com.mojang:brigadier:1.3.10'
    compileOnly 'com.mojang:datafixerupper:8.0.16'
    compileOnly 'org.joml:joml:1.10.5'
}

jar {
    manifest {
        attributes 'Main-Class': 'io.github.nickid2018.genwiki.GenerateWikiData'
    }
}

shadowJar {
    archiveFileName.set("GenWiki.jar")
}

void addRunTask(name, mode, dry = false) {
    tasks.register(name, JavaExec) {
        if (!file("run").isDirectory())
            mkdir("run")
        if (file("run/remapped").isDirectory())
            delete(fileTree("run/remapped"))
        workingDir = file("run")
        if (environment["MAKE_VERSION"] == null)
            environment["MAKE_VERSION"] = ""
        classpath = layout.buildDirectory.files("libs/GenWiki.jar")
        mainClass = "io.github.nickid2018.genwiki.GenerateWikiData"
        if (dry) {
            args(environment["MAKE_VERSION"], "--mode", mode, "--not-run")
        } else {
            args(environment["MAKE_VERSION"], "--mode", mode)
        }
        standardInput = System.in
    }.configure {
        dependsOn(shadowJar)
    }
}

addRunTask("run", "AUTOVALUE")
addRunTask("runStatistics", "STATISTICS")
addRunTask("runStatisticsNotRun", "STATISTICS", true)
addRunTask("runDataGenerator", "CLIENT_DATA")
addRunTask("runISO", "ISO")
addRunTask("runISONotRun", "ISO", true)